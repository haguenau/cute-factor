#! /usr/bin/perl -wCSD
## $Id$

use strict;
use Getopt::Long;
use Pod::Usage;
use utf8;

my ($help) = (0);
my ($fmt_latex, $fmt_unicode) = (0, 1);
GetOptions (
    'latex!' => \$fmt_latex,
    'utf8!' => \$fmt_unicode,
    'h|help' => \$help,
    ) or pod2usage(2);
pod2usage(1) if $help;

=encoding utf-8

=head1 NAME

pretty-factor

=head1 SYNOPSIS

pretty-factor [options] INTEGER

 Options:

   --latex              Print exponents in LaTeX format
   --utf8               Print exponents as plaintext (default)

   -h, --help           Print this help message

=head1 DESCRIPTION

B<pretty-factor> takes an integer, and prints its factorization into
primes in a semi-pretty way; e.g., for C<pretty-factor 1089>:

  3² × 11²

Or, with C<pretty-factor --latex 1089>:

  3^{2} \times 11^{2}

This uses the B<factor> command (from GNU coreutils) to actually
factor.

=cut

sub factor($) {
    my $n = shift;
    my $cmd = 'factor';
    my $line = `$cmd $n`;
    die "Could not execute command \`$cmd'. Install it?\n" if $? < 0;
    die "Running command \`$cmd' failed.\n" if $? > 0;

    my @factors;
    if ($line =~ /^\d+:\s*(.*)$/) {
        @factors = split /\s+/, $1;
    } else {
        die "Broken output from \`factor': $line";
    }

    my %factors = ();
    for my $f (@factors) {
        ++$factors{$f};
    }

    return \%factors;
}

sub pretty_power($$) {
    my ($n, $p) = @_;
    return $n if $p == 1;

    return sprintf '%d^{%d}', $n, $p if $fmt_latex;

    my $super_digits = '⁰¹²³⁴⁵⁶⁷⁸⁹';
    my $super_p =
        join '',
        map { substr $super_digits, ord($_) - ord '0', 1 }
        split //, $p;
    return sprintf '%d%s', $n, $super_p;
}

sub pretty_factors($) {
    my $factors = shift;

    my @members = ();
    for my $n (sort { $a <=> $b } keys %$factors) {
        push @members, pretty_power $n, $factors->{$n};
    }

    my $joiner = $fmt_latex ? ' \times ' : ' × ';
    return join $joiner, @members;
}

sub main() {
    my $n = $ARGV[0];
    die "Args: INTEGER.\n" unless $n =~ '^\d+$';
    my $factors = factor $n;
    print pretty_factors $factors, "\n"
}

main;
